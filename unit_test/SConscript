# Copyright 2011-2013, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#
import os

Import('env')

# Call this function to verify there is a valid developer license to run the unit test.
def have_developer_license():
    from ctypes import windll, byref, create_string_buffer
    have_valid_license = False
    try:
        filetime = create_string_buffer(8)    # buffer the size of a FILETIME structure
        if (0 == windll.wsclient.CheckDeveloperLicense(byref(filetime))):
            have_valid_license = True
    except:
        print('Warning: No valid Windows Store developer license detected, unit tests will not be run.')
        pass
    return have_valid_license


if(not(env.has_key('GTEST_DIR'))):
    print('GTEST_DIR not specified skipping alljoyn_core unit test build')

elif( env['OS'] == 'darwin' and ( env['CPU'] == 'arm' or env['CPU'] == 'armv7' or env['CPU'] == 'armv7s' ) ):
    # do not even try Google test if darwin and arm
    print 'GTEST_DIR ignored when building for OS=darwin CPU=arm, skipping alljoyn_core unit test build'

else:
    gtest_env = env.Clone();
    gtest_dir = gtest_env['GTEST_DIR']
    vars = Variables();
    vars.AddVariables(('GTEST_HOME', '', gtest_dir))
    vars.Update(gtest_env)

    if gtest_dir == '/usr':
        gtest_src_base = '%s/src/gtest' % gtest_dir
    else:
        gtest_src_base = gtest_dir

    if(env['OS_CONF'] == 'android'):
        # Set the ABI based on the CPU
        if(env['CPU'] == 'arm'):
            android_eabi = 'armeabi'
        else:
            android_eabi = 'x86'
        # Set the CPP PATH based on NDK version
        if env.subst('$ANDROID_NDK_VERSION') in [ '8b', '8c', '8d' ]:
            android_cpp_path = '$ANDROID_NDK/sources/cxx-stl/gnu-libstdc++/4.6'
        elif env.subst('$ANDROID_NDK_VERSION') in [ '7',  '8']:
            android_cpp_path = '$ANDROID_NDK/sources/cxx-stl/gnu-libstdc++'
        else:
            print 'common unit test build requires NDK v7 or newer'
            android_cpp_path = None
    else:
        android_cpp_path = None
    if( gtest_env['OS_CONF'] == 'windows'):
        # gTest does not require the same CPPDEFINES as AllJoyn core.
        gtest_env.Append(CPPDEFINES = ['WIN32', '_LIB'])
        # don't use the _DEBUG define unless the /MDd compiler flag is specified
        #gtest_env.Append(CPPDEFINES = ['WIN32', '_DEBUG', '_LIB'])
        gtest_env.Append(CXXFLAGS = ['/EHsc'])
        
    # tr1::tuple is not avalible for android or darwin
    if(env['OS_CONF'] == 'android' or env['OS_CONF'] == 'darwin'):
        gtest_env.Append(CPPDEFINES = ['GTEST_HAS_TR1_TUPLE=0'])
    # clone() library function is NOT available on android-x86
    if(env['OS_CONF'] == 'android' and env['CPU'] == 'x86'):
        gtest_env.Append(CPPDEFINES = ['GTEST_HAS_CLONE=0'])
    # Microsoft Visual Studio 2012 has a different _VARIADIC_MAX default value.
    # See: http://blogs.msdn.com/b/vcblog/archive/2011/09/12/10209291.aspx
    if(env['MSVC_VERSION'] == '11.0'):
        gtest_env.Append(CPPDEFINES = ['_VARIADIC_MAX=10'])
    #we compile with no rtti and we are not using exceptions. 
    gtest_env.Append(CPPDEFINES = ['GTEST_HAS_RTTI=0'])
    # we replace AllJoyn's include CPPPATH options.  AllJoyn includes stlport that will cause the 
    # gTest code to not compile as expected at this time
    gtest_env.Replace(CPPPATH = [ gtest_src_base ])
    if gtest_dir != '/usr':
        gtest_env.Append(CPPPATH = [ gtest_env.Dir('$GTEST_DIR/include') ])


    if(env['OS_CONF'] == 'android'):
        # Android include paths
        gtest_env.Append(CPPPATH=['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL_NDK/arch-%s/usr/include' % env['CPU'],
                                  #Using GNU STL instead of STLPORT
                                  '%s/include' % android_cpp_path,
                                  '%s/include/backward' % android_cpp_path,
                                  '%s/libs/%s/include' % (android_cpp_path, android_eabi)])

    gtest_obj = gtest_env.StaticObject(target = 'gtest-all', source = [ '%s/src/gtest-all.cc' % gtest_src_base ])
    gtest_env.StaticLibrary(target = 'gtest', source = gtest_obj)
    
    test_src = env.Glob('*.cc')

    unittest_env = env.Clone()

    gtest_dir = unittest_env['GTEST_DIR']
    if gtest_dir != '/usr':
        unittest_env.Append(CPPPATH = [gtest_dir + '/include'])

    if(env['OS_GROUP'] == 'windows'):
        unittest_env.Append(CXXFLAGS = ['/EHsc'])

    if(env['OS_CONF'] == 'android'):
        # Android include paths
        unittest_env.Append(CPPPATH=['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL_NDK/arch-%s/usr/include' % env['CPU'],
                                     #Using GNU STL instead of STLPORT
                                     '%s/include' % android_cpp_path,
                                     '%s/include/backward' % android_cpp_path,
                                     '%s/libs/%s/include' % (android_cpp_path, android_eabi),
                                     '$ANDROID_SRC/external/bluetooth/bluez/include',
                                     '$ANDROID_SRC/external/openssl/include'])

    #we compile with no rtti and we are not using exceptions.
    unittest_env.Append(CPPDEFINES = ['GTEST_HAS_RTTI=0'])
    
    # tr1::tuple is not avalible for android or darwin
    if(env['OS_CONF'] == 'android' or env['OS_CONF'] == 'darwin'):
        unittest_env.Append(CPPDEFINES = ['GTEST_HAS_TR1_TUPLE=0'])
    if(env['OS_CONF'] == 'android' and env['CPU'] == 'x86'):
        unittest_env.Append(CPPDEFINES = ['GTEST_HAS_CLONE=0'])
    if(env['MSVC_VERSION'] == '11.0'):
        unittest_env.Append(CPPDEFINES = ['_VARIADIC_MAX=10'])
    #gtest library file is placed on folder above the the object files.
    unittest_env.Append(LIBPATH = ['./'])
    #the linker must be able to find stlport for android
    if(env['OS_CONF'] == 'android'):
        unittest_env.Append(LIBPATH = ['$ANDROID_NDK/platforms/android-$ANDROID_API_LEVEL_NDK/arch-%s/usr/lib' % env['CPU']])
    unittest_env.Prepend(LIBS = ['gtest'])

    obj = unittest_env.Object(test_src);
        
    unittest_prog = unittest_env.Program('ajtest', obj)
    unittest_env.Install('$TESTDIR/bin', unittest_prog)

    #install gtest utilities
    env.Install('$TESTDIR/bin', Dir('test_report').srcnode())

if (env['OS'] == 'win8' and
    env['WINRT_UNITTEST'] == 'on' and
    have_developer_license() and
    (env['CPU'] == 'x86' or env['CPU'] == 'x86_64')):

    appx_env = env.Clone();

    # choose the correct build flavor
    if appx_env['CPU'] == 'x86':
        platform = 'x86'
    elif appx_env['CPU'] == 'x86_64':
        platform = 'x64'

    # Change the name slightly when building a debug version
    if appx_env['VARIANT'].lower() == 'debug':
        debug_name = '_Debug'
    else:
        debug_name = ''

    # this magic setting is needed for vstest.console.exe to run for the right processor architecture
    if appx_env['HOST_ARCH'] == 'x86_64':
        appx_env['ENV']['PROCESSOR_ARCHITECTURE'] = 'AMD64'
    elif appx_env['HOST_ARCH'] == 'x86':
        appx_env['ENV']['PROCESSOR_ARCHITECTURE'] = 'X86'


    unit_test_appx_name_stem = 'winrt/AllJoynUnitTests/AppPackages/AllJoynUnitTests_1.0.0.0_' + platform + debug_name + '_Test/AllJoynUnitTests_1.0.0.0_' + platform + debug_name + '.appx'
    unit_test_appx_name = '../../../../alljoyn_core/unit_test/' + unit_test_appx_name_stem


    # make_appx_cmd:
    #    produces the appx files with the unit tests inside,
    #    depends on AllJoyn.dll to exist
    #    runs msbuild on the AllJoynUnitTests solution file.
    make_appx_cmd = appx_env.Command(
                          unit_test_appx_name,
                        [   # source files to depend upon
                            'winrt/AllJoynUnitTests.sln',
                            '$DISTDIR/bin/AllJoyn.dll',
                            '$DISTDIR/bin/AllJoyn.winmd'
                        ],
                        'msbuild.exe ${SOURCE.abspath} /t:Clean,Rebuild /p:Configuration=' + appx_env['VARIANT'] + ';Platform=' + platform,
                        )

    # the unit test:
    #    produces no files,
    #    depends on the .appx to exist
    #    runs vstest.console.exe on the appx file.
    run_appx_unit_test = appx_env.Command( None,
                        unit_test_appx_name_stem,
                        'vstest.console.exe "${SOURCE.abspath} " /InIsolation /platform:' + platform
                        )

#    # Install vsix at the same level as the architecture directory
#    makeappx_install = appx_env.Install('$DISTDIR/../test', source = unit_test_appx_name_stem )
#    appx_env.Depends(makeappx_install, make_appx_cmd)


    # make sure the appx file is built before trying to run the unit test.
    appx_env.Depends(run_appx_unit_test, make_appx_cmd)
