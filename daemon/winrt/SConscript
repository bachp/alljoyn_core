# Copyright 2010 - 2011, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
# 

import os
Import('env', 'daemon_objs', 'common_objs', 'alljoyn_core_objs', 'bdobj')

# Add OS specific daemon_objs
os_objs = env.Object(['ProximityScanner.cc', 'Socket.cc'])

# TODO: Fix our dependencies so they don't "bleed" all over
env.Append(CPPPATH=['..'])

proximity_objs = SConscript('proximity/SConscript')
winmdswitch = '\"/WINMDFILE:%s\"' % os.path.abspath('AllJoyn.winmd')
env.Append(LINKFLAGS=['/MANIFEST:NO', '/SUBSYSTEM:CONSOLE', '/TLBID:1', '/APPCONTAINER', '/WINMD', winmdswitch])

if env['CPU'] == 'arm':
    env.Append(LINKFLAGS=['/INCREMENTAL:NO'])

# Build the resources for the dll
rsc = env.RES('DaemonResources.rc')

# Build the Windows daemon.
alljoyn_libs = env.SharedLibrary('AllJoyn', daemon_objs + common_objs + alljoyn_core_objs + proximity_objs + os_objs)
env.Install('$DISTDIR/lib', alljoyn_libs[1]) #put the *.lib file in the lib folder
env.Install('$DISTDIR/bin', 'AllJoyn.winmd') #put the *.winmd file in the bin folder

progs = [alljoyn_libs[0]] # alljoyn.dll will be returned
Return('progs', 'os_objs')
